/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT PLUGINS/FUNCTIONS/MODULES/SUBWORKFLOWS/WORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
// MODULES
include { CSVTK_FILTER2                 } from '../../../modules/local/csvtk/filter2'
include { DIAMOND_BLASTP                } from '../../../modules/local/diamond/blastp'
include { DIAMOND_BLASTPSELF            } from '../../../modules/local/diamond/blastpself'
include { DIAMOND_MAKEDB                } from '../../../modules/local/diamond/makedb'
include { MCL                           } from '../../../modules/local/mcl'
include { PROTEINSIMILARITY_SELFSCORE   } from '../../../modules/local/proteinsimilarity/selfscore'
include { PROTEINSIMILARITY_NORMSCORE   } from '../../../modules/local/proteinsimilarity/normscore'
include { SEQKIT_SPLIT2                 } from '../../../modules/local/seqkit/split2'
include { UHVDB_CATNOHEADER             } from '../../../modules/local/uhvdb/catnoheader'

// SUBWORKFLOWS
include { MCLCLUSTER as MCLCLUSTER_SUBFAMILY    } from '../../../subworkflows/local/mclcluster'
include { MCLCLUSTER as MCLCLUSTER_GENUS        } from '../../../subworkflows/local/mclcluster'
include { MCLCLUSTER as MCLCLUSTER_SUBGENUS     } from '../../../subworkflows/local/mclcluster'

workflow AAICLUSTER {

    take:
    taxa_virus_fna_gz   // channel: [ [ meta ], virus.taxa_reps.fna.gz ]

    main:

    if (file("${params.uhvdb_dir}/*_species_reps.fna.gz").isEmpty()) {
        log.warn "UHVDB species reps fasta does not exist: ${params.uhvdb_dir}/*_species_reps.fna.gz"
        log.warn "Performing all-v-all AAIcluster."
        ch_ref_fna_gz = taxa_virus_fna_gz
    } else {
        ch_ref_fna_gz = channel.fromPath("${params.uhvdb_dir}/*_species_reps.fna.gz")
            .map { fna_gz -> [ [ id: 'uhvdb_species_reps' ], fna_gz ] }
    }

    //-------------------------------------------
    // MODULE: SEQKIT_SPLIT
    // inputs:
    // - [ [ meta ], virus.taxa_reps.fna.gz ]
    // outputs:
    // - [ [ meta ], [ virus.taxa_reps.part_*.fna.gz ... ] ]
    // steps:
    // - Split fasta into chunks (script)
    //--------------------------------------------
    SEQKIT_SPLIT2(
        taxa_virus_fna_gz,
        params.fasta_chunk_size
    )
    ch_taxa_split_viruses_fna_gz = SEQKIT_SPLIT2.out.fastas_gz
        .map { _meta, fna_gzs -> fna_gzs }
        .flatten()
        .map { fna_gz ->
            def taxa = fna_gz.getBaseName().toString() =~ /taxa([^\.]+)\.part/
            [ [ id: fna_gz.getBaseName(), taxa: taxa[0][1] ], fna_gz ]
        }

    //
    // MODULE: Create DIAMOND database reference
    //
    DIAMOND_MAKEDB(
        ch_ref_fna_gz
    )

    //
    // MODULE: Run DIAMOND for each split against the reference 
    //
    ch_diamond_blastp_input = ch_taxa_split_viruses_fna_gz.map { meta, fna_gz -> [ meta.taxa, meta, fna_gz ] }
        .combine(DIAMOND_MAKEDB.out.dmnd.map { meta, fna_gz -> [ meta.taxa, meta, fna_gz ] }, by:0)
        .map { _taxa, meta, fna_gz, _meta2, dmnd -> [ meta, fna_gz, dmnd ] }
    DIAMOND_BLASTP(
        ch_diamond_blastp_input
    )

    //
    // MODULE: Run DIAMOND against self to calculate self scores
    //
    DIAMOND_BLASTPSELF(
        DIAMOND_BLASTP.out.faa_gz
    )

    //
    // MODULE: Calculate self scores
    //
    PROTEINSIMILARITY_SELFSCORE(
        DIAMOND_BLASTPSELF.out.tsv_gz
    )

    //
    // MODULE: Calculate normalized bitscore
    //
    ch_normscore_input = PROTEINSIMILARITY_SELFSCORE.out.tsv_gz
        .combine(DIAMOND_BLASTP.out.tsv_gz, by:0)
    PROTEINSIMILARITY_NORMSCORE(
        ch_normscore_input
    )

    //-------------------------------------------
    // MODULE: UHVDB_CATNOHEADER
    // inputs:
    // - [ [ meta ], [ norm_score.part_*.tsv.gz ... ] ]
    // outputs:
    // - [ [ meta ], aaicluster.tsv.gz ]
    // steps:
    // - Combine files (script)
    //--------------------------------------------
    // TODO: groupTuple by taxa here
    ch_catnoheader_input = PROTEINSIMILARITY_NORMSCORE.out.tsv_gz.map { meta, tsv_gz -> [ meta.taxa, meta, tsv_gz ] }.groupTuple().map { meta_taxa, _meta, tsv_gz -> [ [ id:meta_taxa ], tsv_gz, 'tsv.gz' ] }
    UHVDB_CATNOHEADER(
        ch_catnoheader_input,
        "${params.output_dir}/${params.new_release_id}/compare/aaicluster/"
    )

    //-------------------------------------------
    // MODULE: CSVTK_FILTER2
    // inputs:
    // - [ [ meta ], aaicluster.tsv.gz ]
    // outputs:
    // - [ [ meta ], aaicluster.pruned.tsv.gz ]
    // steps:
    // - Filter matrix (script)
    //--------------------------------------------
    CSVTK_FILTER2(
        UHVDB_CATNOHEADER.out.combined,
        5.5
    )

    //-------------------------------------------
    // MODULE: MCL
    // inputs:
    // - [ [ meta ], aaicluster.pruned.tsv.gz ]
    // outputs:
    // - [ [ meta ], aaicluster.mcl.gz ]
    // steps:
    // - Decompress (script)
    // - Run MCL (script)
    //--------------------------------------------
    MCL(
        CSVTK_FILTER2.out.tsv_gz,
        "${params.output_dir}/${params.new_release_id}/compare/aaicluster/family_mcl/"
    )

    //-------------------------------------------
    // SUBWORKFLOW: MCLCLUSTER_SUBFAMILY
    // inputs:
    // - [ [ meta ], aaicluster.pruned.tsv.gz ]
    // - [ [ meta ], aaicluster.mcl.gz ]
    // - similarity_threshold
    // outputs:
    // - [ [ meta ], aaicluster.mcl.gz ]
    // steps:
    // - UHVDB_PRUNE (module)
    // - MCL (script)
    //--------------------------------------------
    MCLCLUSTER_SUBFAMILY(
        CSVTK_FILTER2.out.tsv_gz,
        MCL.out.mcl_gz,
        32.0,
        "${params.output_dir}/${params.new_release_id}/compare/aaicluster/subfamily_mcl/"
    )

    //-------------------------------------------
    // SUBWORKFLOW: MCLCLUSTER_GENUS
    // inputs:
    // - [ [ meta ], aaicluster.pruned.tsv.gz ]
    // - [ [ meta ], aaicluster.mcl.gz ]
    // - similarity_threshold
    // outputs:
    // - [ [ meta ], aaicluster.mcl.gz ]
    // steps:
    // - UHVDB_PRUNE (module)
    // - MCL (script)
    //--------------------------------------------
    MCLCLUSTER_GENUS(
        CSVTK_FILTER2.out.tsv_gz,
        MCLCLUSTER_SUBFAMILY.out.mcl_gz,
        65.0,
        "${params.output_dir}/${params.new_release_id}/compare/aaicluster/genus_mcl/"
    )

    //-------------------------------------------
    // SUBWORKFLOW: MCLCLUSTER_SUBGENUS
    // inputs:
    // - [ [ meta ], aaicluster.pruned.tsv.gz ]
    // - [ [ meta ], aaicluster.mcl.gz ]
    // - similarity_threshold
    // outputs:
    // - [ [ meta ], aaicluster.mcl.gz ]
    // steps:
    // - UHVDB_PRUNE (module)
    // - MCL (script)
    //--------------------------------------------
    MCLCLUSTER_SUBGENUS(
        CSVTK_FILTER2.out.tsv_gz,
        MCLCLUSTER_GENUS.out.mcl_gz,
        80.0,
        "${params.output_dir}/${params.new_release_id}/compare/aaicluster/subgenus_mcl/"
    )

    // emit:
    // proteinsimilarity_tsv_gz = PROTEINSIMILARITY_COMBINE.out.tsv_gz
}
